#!/bin/sh

awk_max='
BEGIN { max1 = 0; max2 = 0; max3 = 0 }
{
    if (length($0) && substr($0, 1, 1) != " ") {
        if (length($1) > max1) max1 = length($1)
        if (length($2) > max2) max2 = length($2)
        gsub(/ seconds?/, "s", $3)
        gsub(/ minutes?/, "m", $3)
        gsub(/ hours?/,   "h", $3)
        gsub(/ days?/,    "d", $3)
        gsub(/ weeks?/,   "w", $3)
        gsub(/ months?/,  "mo", $3)
        gsub(/ years?/,   "y", $3)
        if (length($3) > max3) max3 = length($3)
    }
}
END { print max1, max2, max3 }
'

awk_parse='
BEGIN { max1 = int(max1); max2 = int(max2); max3 = int(max3) }
{
    if (length($0) && substr($0, 1, 1) != " ") {
        message = sprintf("%-" max1 "s", $1)
        author = sprintf("%-" max2 + 1 "s", $2 ",")
        time = $3
        date = $4

        gsub(/ seconds?/, "s", time)
        gsub(/ minutes?/, "m", time)
        gsub(/ hours?/,   "h", time)
        gsub(/ days?/,    "d", time)
        gsub(/ weeks?/,   "w", time)
        gsub(/ months?/,  "mo", time)
        gsub(/ years?/,   "y", time)
        time = sprintf("%-" max3 + 3 "s", "(" time ")," )

        changes = "0 files changed"
        insertions = 0
        deletions = 0

        getline
        split($0, parts, ",")
        for (i = 1; i <= length(parts); i++) {
            gsub(/^[ \t]+|[ \t]+$/, "", parts[i])
            if (match(parts[i], /[0-9]+/)) {
                value = substr(parts[i], RSTART, RLENGTH)
            }
            if (match(parts[i], /files? changed/)) {
                changes = parts[i]
            }
            else if (match(parts[i], /insertion/)) {
                insertions = value
            }
            else if (match(parts[i], /deletion/)) {
                deletions = value
            }
        }

        if (insertions > 0 && deletions > 0) {
            changes = changes " (+" insertions ", -" deletions ")"
        }
        else if (insertions > 0) {
            changes = changes " (+" insertions ")"
        }
        else if (deletions > 0) {
            changes = changes " (-" deletions ")"
        }

        print message " " author " " date " " time " " changes
    }
}
'

stats=$(git log -5 $@ --pretty=format:'%s|%an|%ar|%ad' --date=format:'%d.%m.%Y' --shortstat)
read max1 max2 max3 <<<$(echo "$stats" | awk -F'|' "$awk_max")
echo "$stats" | awk -v max1="$max1" -v max2="$max2" -v max3="$max3" -F'|' "$awk_parse"
